Fórmulas DAX (Data Analysis Expressions | Expressões de Análise de Dados)

---------------------------------------------------------------------------------------------------------


02-introducaoAsFormulasDAX

Duas Maneiras de usar DAX:

1- Colunas Calculadas
	Vamos criar uma coluna gerada por meio de combinação de cálculos entre outra(s) tabela(s)

2- Medidas
	Calculos feitos que não aparecem na tabela mas são usados nos relatórios


---------------------------------------------------------------------------------------------------------


03-introducaoAColunasCalculadas-parte1

Para adicionar uma coluna calculada podemos ir no Menu Superior -> 'Página Inicial/Home' -> 'Nova Coluna'
ou clicamos com o segundo botão na última coluna da tabela e vamos na opção 'Nova Coluna'

Após a criação da Nova Coluna utilizaremos a barra de fórmula com a coluna toda selecionada para editar seu nome e criar a lógica de seu preenchimento

Exemplo:
	Tipo do Pedido = if('Base Vendas 2016 - 2018'[Tamanho Pedido]>1, "Pedido Múltiplo", "Pedido Único")


---------------------------------------------------------------------------------------------------------


05-introducaoAMedidas

Quando o cálculo não for linha a linha e resultar em um valor único não devemos criar uma coluna nova calculada mas sim o que chamamos no PowerBI de 'medida'

A medida vai criar uma 'variável' que poderá ser utilizada dentro dos relatórios/dashboards do PowerBI

Para criar uma nova medida vamos no Menu Superior -> 'Página Inicial/Home' -> 'Nova Medida'
Agora vamos criar por meio da barra de Fórmulas a medida desejada, de maneira análoga à criação de colunas.
Exemplo:
	Total Pedidos = SUM('Base Vendas 2016 - 2018'[Tamanho Pedido])

Observe que será criado o nome Total Pedidos dentro da aba 'Base Vendas 2016 - 2018' embora não seja uma coluna, podendo ser usada para cálculos e demonstrações no dashboard. Apenas na aba 'Exibição de Relatório' que poderemos ver o valor da Medida

Podemos aplicar filtros nas medidas normalmente, como separar o 'Total Pedidos' em Homens e Mulheres na aba de relatório do PowerBI



*** EM GERAL 

usamos o cálculo de colunas para calcular novos critérios de criação de análises, ou seja, LINHAS/COLUNAS de um relatório

usamos o cálculo de medidas geralmente para efetuar calculos numéricos entrando normalmente na parte de VALORES da matriz


---------------------------------------------------------------------------------------------------------


06-recapitulando-colunasCalculadas X Medidas

Colunas Calculadadas
	- Os valores são calculados baseados na informação de cada linha da tabela (são calculados individualmente pra cada linha, chamado de 'Contexto de Linha')
	- Acrescenta uma nova coluna na tabela
	- Recalcula de acordo com alterações nas células
	- Em geral são usadas nos campos de LINHA, COLUNA ou FILTROS nas Matrizes/Gráficos


Medidas
	- Os valores são calculados baseados nos filtros aplicados nas Matrizes (chamado de 'Contexto de Filtro')
	- Não acrescenta novas colunas nas tabelas
	- Recalcula de acordo com alterações nos filtros do Relatório (em uma Matriz ou Gráfico)
	- Quase sempre usado no campo VALORES de uma Matriz/Gráfico


---------------------------------------------------------------------------------------------------------


09-formulasDeData-parte1

Criando a coluna 'Ano' na tabela calendário:

	Ano = YEAR('Calendário'[Data])
ou
	Ano = 'Calendário'[Data].[Ano]

	Mês = MONTH('Calendário'[Data])
ou
 	Mês = 'Calendário'[Data].[MonthNo] (mês numérico)
	Mês = 'Calendário'[Data].[Mês] (mês literal)


---------------------------------------------------------------------------------------------------------


10-formulasDeData-parte2

Extraindo o dia da semana de uma data:
	
	Dia da Semana = WEEKDAY('Calendário'[Data],opcao)
	onde opcao = 1, 2 ou 3
	opção 1 - Dia da semana de 1 a 7, começando no Domingo e terminando no Sábado
	opção 2 - Dia da semana de 1 a 7, começando na Segunda e terminando no Domingo
	opção 3 - Dia da semana de 0 a 6, começando na Segunda e terminando no Domingo 


---------------------------------------------------------------------------------------------------------


11-formulasDeData-parte3

Calculando o último dia do mês referente a uma data:

	Último dia do mês = EOMONTH('Base Vendas'[Data da Venda],opcao)
	onde opcao = 0, 1, 2, 3...
	quando opcao=0 será o último dia do mês no próprio mês onde está a data,
	quando opcao=1 será o ultimo dia do mês no mês seguinte onde está a data,
	quando opcao=2 será o último dia do mês no mês atual + 2 de onde está a data,
	e assim adiante seguindo essa lógica

Calculando diferença entre datas:

	Usando o exemplo de idade:

	Idade = DATEDIFF('Cadastro Clientes'[Data de Nascimento],TODAY(),YEAR)
	onde os argumentos serão:
	(Data Inicial, Data Final, Unidade de tempo desejada)
	A unidade de tempo pode ser dias, meses, anos, segundos, etc


---------------------------------------------------------------------------------------------------------


12-funcaoIf

Podemos utilizar a condicional IF no DAX, segue um exemplo:

	Categoria de Cliente = IF('Cadastro Clientes'[Idade]>40,"Categoria 1","Categoria 2")
	onde os argumentos são:
	(Condição, Resultado se True, Resultado se False) 


---------------------------------------------------------------------------------------------------------


13-funcaoIfComposta-aninhada

Podemos utilizar a condicional IF no DAX também de forma composta(aninhada):

	Exemplo:

	Categoria de Cliente = IF('Cadastro Clientes'[Idade]>=60,"Categoria 1",
    		IF('Cadastro Clientes'[Idade]>=45,"Categoria 2",
        		IF('Cadastro Clientes'[Idade]>=30,"Categoria 3","Categoria 4")))

Obs: A indentação é opcional e foi colocada apenas para melhorar o entendimento


---------------------------------------------------------------------------------------------------------


14-ifComVariasCondicoes-OR-parte1

Usando o IF no DAX com várias condições (temos mais de uma maneira), o operador de igual(=) pode ser usado de maneira simples (=) ou em dupla (==) igualmente darão o mesmo resultado:

	Exemplo da utilização do Operador OR ou '||':

	Fim de Semana = IF('Calendário'[Dia da Semana]==1 || 'Calendário'[Dia da Semana]==7,"Fim de Semana","Dia de Semana")

	ou

	Fim de Semana = IF(OR('Calendário'[Dia da Semana]==1,'Calendário'[Dia da Semana]==7),"Fim de Semana","Dia de Semana")


---------------------------------------------------------------------------------------------------------


16-funcaoIfComVariasCondicoes-AND-parte1

Usando o IF no DAX com várias condições (temos mais de uma maneira), o operador de igual(=) pode ser usado de maneira simples (=) ou em dupla (==) igualmente darão o mesmo resultado:

	Exemplo da utilização do Operador AND ou '&&':

	Promoção dia das Mães = IF('Cadastro Clientes'[Sexo]=="Feminino" && 'Cadastro Clientes'[Idade]>=50,"Sim","Não")
	
	ou

	Promoção dia das Mães = IF(AND('Cadastro Clientes'[Sexo]=="Feminino",'Cadastro Clientes'[Idade]>=50),"Sim","Não")


---------------------------------------------------------------------------------------------------------


19-funcaoConcatenate

Utilizada para concatenar(juntar) textos

	Exemplo (observe que a função CONCATENATE permite apenas dois argumentos, ou seja, para que seja utilizada para mais de dois textos teremos que chamá-la de maneira RECURSIVA):

	Nome Completo = CONCATENATE(CONCATENATE('Cadastro Clientes'[Primeiro Nome]," "),'Cadastro Clientes'[Sobrenome])

	ou

	Nome Completo = 'Cadastro Clientes'[Primeiro Nome] & " " & 'Cadastro Clientes'[Sobrenome]


---------------------------------------------------------------------------------------------------------


21-funcoesLeftEUpper-aplicacao

A função LEFT faz um recorte no texto nos N primeiros caracteres de um texto, onde N deve ser informado pelo usuário.

	Exemplo, retornando o nome do mês abreviado (apenas os três primeiros caracteres):
	
	Mês Abreviado = LEFT('Calendário'[Nome do Mês],3)

	Agora colocando o mês abreviado em maiúsculo:

	Mês Abreviado = UPPER(LEFT('Calendário'[Nome do Mês],3))


---------------------------------------------------------------------------------------------------------


23-funcoesRightEMiddle-aplicacao

A função RIGHT faz um recorte no texto nos N últimos caracteres de um texto, onde N deve ser informado pelo usuário (caso não seja informado o default de N será igual a 1)

	Exemplo, retornando o código do produto apenas com os números (ultimos 4 caracteres):

	Numeração do Produto = RIGHT('Cadastro Produtos'[SKU],4)
	(na falta do segundo argumento ele será considerado igual a 1)


A função MID faz um recorte no texto nos caracteres em sequencia desejados conforme informados nos argumentos da função.

	Exemplo, retornando o código do produto apenas com os números (conforme o exemplo anterior):

	Numeração do Produto = MID('Cadastro Produtos'[SKU],3,4)
	onde MID(Texto,Posição Caractere Inicial, Número de Caracteres)

	**Observação: Podemos usar o argumento número de caracteres com um número MUITO grande caso desejarmos capturar o texto até o fim e o número de caracteres até o fim for desconhecido, ele vai capturar apenas até o último caractere do texto:

	Exemplo:
	
	Numeração do Produto = MID('Cadastro Produtos'[SKU],3,1000)


---------------------------------------------------------------------------------------------------------


24-formulaRelated

Consegue transferir colunas de uma tabela para outra tabela, a formula RELATED SÓ FUNCIONA se as tabelas estiverem relacionadas dentro do ambiente de relacionamento (aba exibição de modelo)

	Exemplo:
	
	Preço do Produto = RELATED('Cadastro Produtos'[Preço Unitário])

	Assim criaremos na Tabela de BaseVendas uma coluna com os preços dos produtos que estavam na tabela 'Cadastro Produtos'


---------------------------------------------------------------------------------------------------------


26-funcaoSum

Utilizada para fazer o somatorio dos valores de uma coluna, GERALMENTE seu resultado será uma medida, essa medida ficará atrelada a uma tabela e terá um icone de calculadora mostrando que é resultando de algum cálculo.

	Exemplo:

	Faturamento Total = SUM('Base Vendas 2016 - 2018'[Faturamento na Venda])

	Obs: Podemos criar a medida também na aba 'Exibição de Relatório' clicando com o segundo botão do mouse na tabela de sejada e na opção 'Nova Medida'

	Na aba 'Exibição de Dados' clicaremos com o segundo botão do mouse em qualquer coluna e iremos na opção 'Nova Medida' para criar uma nova medida


---------------------------------------------------------------------------------------------------------


27-funcoesDeDivisao-aplicandoMedidasEmMatrizes

Por mais que a medida seja um único valor, ainda assim podemos utilizá-la para segregar seu valor em diversas categorias, como por exemplo na medida 'Faturamento Total' aplicar em linhas de uma matriz o 'Nome do Produto' para saber o valor do faturamento por cada produto vendido

Como efetuar divisão:
	
	Exemplo:

		Taxa de Devoluções = [Total de Devoluções] / [Total Pedidos]

		ou

		Taxa de Devoluções = DIVIDE([Total de Devoluções],[Total Pedidos])


---------------------------------------------------------------------------------------------------------


28-funcaoAverage

Utilizamos a funcao Average para calcular e média de algum valor, como exemplo vamos calcular a média do número de produtos por devolução nas lojas:

Exemplo:

	Média de No de Produtos por Devolução = AVERAGE('Base Devoluções'[Quantidade Devolvida])


---------------------------------------------------------------------------------------------------------


29-funcoesMaxMin

A funções MAX e MIN retornam respectivamente o Maior e Menor valor de um conjunto de dados

Exemplo:

	Máximo Faturado em Uma Venda = MAX('Base Vendas 2016 - 2018'[Faturamento na Venda])

	Mínimo Faturado em Uma Venda = MIN('Base Vendas 2016 - 2018'[Faturamento na Venda])


---------------------------------------------------------------------------------------------------------


31-funcaoCount-parte1

A fórmula COUNT conta os NÚMEROS de uma coluna, o que significa que ela somará +1 a cada célula da coluna que tiver valor NUMÉRICO, ou seja, só funciona para colunas NUMÉRICAS

	Exemplo de uso:

		Número de Vendas = COUNT('Base Vendas 2016 - 2018'[Tamanho Pedido])


---------------------------------------------------------------------------------------------------------


33-funcaoCounta

A fórmula COUNTA conta quantos VALORES existem em uma coluna, independente de seu tipo ser numérico ou não, ou seja, funcionará para todos os tipos de coluna, somará +1 a cada célula da coluna

	Exemplo de uso:

		Número de Clientes = COUNTA('Cadastro Clientes'[Código Cliente])


---------------------------------------------------------------------------------------------------------


34-funcaoDistinctcount

A fórmula DISTINCTCOUNT conta quantos VALORES DISTINTOS existem em uma coluna, ou seja, sem considerar os repetidos.

	Exemplo de uso:

		Quantidade de Produtos Devolvidos = DISTINCTCOUNT('Base Devoluções'[SKU])

	***OBSERVAÇÃO: Em algumas análises podemos necessitar consultar valores que não possuem valores listados ou 'sem valor', se for esse o caso temos que ir até o item dentro do campo/aba de 'Visualizações' -> 'Linhas' -> 'Opções' -> 'Mostrar itens sem dados'


---------------------------------------------------------------------------------------------------------


35-funcaoCountrows

A fórmula COUNTROWS conta quantas LINHAS existem em uma TABELA. Ela faz o cálculo em função de tabela e não coluna

	Exemplo de uso:

		Número de Devoluções = COUNTROWS('Base Devoluções')


---------------------------------------------------------------------------------------------------------


36-formulaCalculate-parte1

A fórmula Calculate efetua qualquer tipo de cálculo onde precisamos especificar um FILTRO ou um critério para que ela ocorra, como por exemplo o somatório de vendas do ano de 2017, ou o número de vendas do Vendedor João.

=CALCULATE(Expressão, Filtro1, Filtro2, ...)

	Expressão: Nome de uma medida que foi calculada ou uma Formula DAX
		Exemplos:
			- [Total Pedidos]
			- SUM('Base Devoluções'[Quantidade Devolvida])

	Filtros: Comparação simples (Verdadeiro/Falso) para filtrar a expressão (precisam ser valores simples e fixos, não é possível criar filtros a partir de medidas)
		Exemplos:
			- 'Base Devoluções'[Loja] = "Rio de Janeiro"
			- 'Base Vendas 2016 - 2018'[Tamanho Pedido] >= 2
			- 'Calendário'[Ano] > 2016

	Exemplos CALCULATE:
	
		- CALCULATE([Total Pedidos],'Calendário'[Ano] > 2016)
			- Calcula o total de pedidos feitos após o ano de 2016

		- CALCULATE(SUM('Base Devoluções'[Quantidade Devolvida]),'Base Devoluções[Loja] = "Rio de Janeiro")
			- Calcula o total de devoluções feitas na Loja do Rio de Janeiro
		
		- Total Vendas de Celular = CALCULATE([Total Pedidos],'Cadastro Produtos'[Categoria] = "Celular")
			- Nesse caso agora podemos estudar mais a fundo os dados das vendas de Celular, antes seria mais dificil efetuar esse estudo

---------------------------------------------------------------------------------------------------------


40-formulaAll

Retorna todas as linhas de uma tabela ou células de uma coluna, desconsiderando e ignorando qualquer filtro, a fórmula ALL será utilizado como parâmetro da função CALCULATE

Basicamente a função ALL FUNCIONA como um filtro, porém um filtro feito para RETIRAR todos os filtros

	= ALL(Tabela ou Coluna, [Coluna1], [Coluna2], ...)

		- Observe que as opções em colchetes são opcionais
		- Só pode adicionar novas colunas se o primeiro argumento também for uma coluna
		- Todas as colunas precisam ser da mesma tabela


---------------------------------------------------------------------------------------------------------


41-formulaCalculateEAll

	Exemplo:
		A fórmula ALL será utilizada como parâmetro da função CALCULATE

		Total Pedidos (Absoluto) = CALCULATE([Total Pedidos],ALL('Base Vendas 2016 - 2018'))

		calcula o total de pedidos valor absoluto , desconsiderando qualquer filtro imposto pela tabela 'Base Vendas 2016 - 2018' ***IMPORTANTE para calculos em que precisamos referenciar o resultado a todos os valores da tabela e nao apenas aos que estão filtrados naquele momento***

		Total Devoluções (Absoluto) = CALCULATE([Total de Devoluções],ALL('Base Devoluções'))


---------------------------------------------------------------------------------------------------------


43-formulaFilter

Retorna uma tabela de acordo com um filtro selecionado, a fórmula FILTER será utilizada como parametro da função CALCULATE
	
	= CALCULATE(Expressao,[Filtro1],...)
		, onde [Filtro1] = FILTER(Tabela,Critério)

	*** Filtro sem usar o FILTER: O filtro do calculate é um filtro comum (onde não está usando o FILTER), este filtro NÃO dá pra usar medidas, só expressões simples.

	Ex: 'Base Devoluções'[Loja]="Rio de Janeiro
		'Calendário'[Ano]>2016


	*** Filtro usando o FILTER: pode usar tanto expressões simples quanto medidas

	Ex:  'Base Vendas 2016 - 2018'[Faturamento de Vendas >= [Média de Faturamento]
		'Base Devoluções'[Loja]="Rio de Janeiro"

	
	Ex: Calculando o número de vendas que foram acima da média de faturamento por venda

		Vendas Acima da Média = CALCULATE(COUNT('Base Vendas 2016 - 2018'[Tamanho Pedido]),FILTER('Base Vendas 2016 - 2018','Base Vendas 2016 - 2018'[Faturamento na Venda]>[Média Absoluta de Faturamento]))


---------------------------------------------------------------------------------------------------------


47-funcoesIteradoras(X)-explicacao

Também conhecidas como fórmulas X, fazem algum calculo linha por linha da tabela e depois aplicam alguma agregação (soma, máximo, mínimo, média, etc)

	Exemplos:
		=SUMX, =COUNTX, =AVERAGEX, =MAXX, =MINX , =RANKX


---------------------------------------------------------------------------------------------------------


48-funcaoSumx

	Exemplo:
		Faturamento Total = SUMX('Base Vendas 2016 - 2018','Base Vendas 2016 - 2018'[Tamanho Pedido] * 'Base Vendas 2016 - 2018'[Preço do Produto])
		Observe que primeiramente ele vai multiplicar cada linha da coluna 'tamanho pedido' ao 'preço do produto' e depois disso fara a soma de cada dessa linhas dessa 'coluna' tamanho pedido * preço do produto


---------------------------------------------------------------------------------------------------------


49-funcaoAveragex

	Exemplo:
		Média de Faturamento por Venda = AVERAGEX('Base Vendas 2016 - 2018','Base Vendas 2016 - 2018'[Preço do Produto] * 'Base Vendas 2016 - 2018'[Tamanho Pedido])


---------------------------------------------------------------------------------------------------------


50-funcaoMaxx

	Exemplo:
		Máximo Faturado em Uma Venda = MAXX('Base Vendas 2016 - 2018','Base Vendas 2016 - 2018'[Tamanho Pedido] * 'Base Vendas 2016 - 2018'[Preço do Produto])


---------------------------------------------------------------------------------------------------------


51-correcaoDaMedidaComAFuncaoFilter

	Correção:
		Vendas Acima da Média = CALCULATE(COUNT('Base Vendas 2016 - 2018'[Tamanho Pedido]),FILTER('Base Vendas 2016 - 2018','Base Vendas 2016 - 2018'[Preço do Produto]*'Base Vendas 2016 - 2018'[Tamanho Pedido] > [Média Absoluta de Faturamento]))


---------------------------------------------------------------------------------------------------------


54-inteligenciaDeTempo-funcaoDatesytd-acumuladoDoAno

Utilizaremos a funcao DATESYTD para calcular o acumulado do ano do faturamento total do ano para o mês em que estamos, isso significa que o resultado será o faturamento acumulado até o mês em que nos encontramos

	Exemplo:
		Janeiro 	= Janeiro
		Fevereiro	= Janeiro + Fevereiro
		Março		= Janeiro + Fevereiro + Março
		...

	Para isso usaremos a formula:
		Faturamento Acumulado do Ano = CALCULATE([Faturamento Total],DATESYTD('Calendário'[Data]))


---------------------------------------------------------------------------------------------------------


55-inteligenciaDeTempo-funcaoDatesmtd-acumuladoDoMes

A funcao DATESMTD funciona de maneira analoga à anterior (DATESYTD) porém teremos o valor acumulado do mês para o dia em que nos encontramos do mês (faturado do mês até aquele momento) 

	Exemplo:
		Total Pedidos Acumulado no Mês = CALCULATE([Total Pedidos],DATESMTD('Calendário'[Data]))


---------------------------------------------------------------------------------------------------------


56-inteligenciaDeTempo-funcaoDateadd-comparacaoEntrePeriodos

A funcao DATEADD efetua uma comparacao entre periodos, entao ela sempre lancará o valor que foi configurado quando atrelado a um valor original

	Exemplo:
		Ano anterior , Mês anterior, Dia posterior, etc

		Buscando informações acerca do faturamento do mês anterior sempre que um mês em questão for demonstrado:
			Faturamento do Mês Anterior = CALCULATE([Faturamento Total],DATEADD('Calendário'[Data],-1,MONTH))
	
			onde -1 = periodo solicitado e MONTH unidade solicitado (o que quer dizer mês anterior)


---------------------------------------------------------------------------------------------------------


57-inteligenciaDeTempo-funcaoDatesinperiod

O objetivo dessa formula é efetuar o calculo do acumulado de alguma medida em um período específico, como por exemplo o acumulado das vendas nos ultimos 10 dias, nos ultimos 5 meses, etc

	Exemplo:
		15 dias de Faturamento = CALCULATE([Faturamento Total],DATESINPERIOD('Calendário'[Data],MAX('Calendário'[Data]),-15,DAY))

		onde:
		[Faturamento Total] = De qual medida queremos extrair os valores desejados
		'Calendário'[Data] = De que tabela vamos retirar o período desejado
		MAX(('Calendário'[Data]) = O ultimo dia do período que desejamos que seja considerado (start date que aqui na verdade utilizaremos o ultimo dia em vez do primeiro)
		-15 = O período que desejamos que seja considerado (negativo pois o start date na verdade é o ultimo dia)
		DAY = A medida referente ao -15 mencionado no argumento anterior (pode ser MONTH, YEAR, etc)


---------------------------------------------------------------------------------------------------------